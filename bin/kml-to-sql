#!/usr/bin/perl
use strict;
use warnings;
use feature 'switch';
use XML::Simple;
use Text::CSV_XS;

my $city_name = 'burnaby' || shift || die;
my $kml_file = "data/$city_name.kml";
die unless -e $kml_file;

my $UTC = '-08';
my $kml = XMLin($kml_file);
my $area_name = ucfirst $city_name;

# use Data::Dumper;
# warn Dumper $kml;
# exit;

my %zones;
my $placemarks = $kml->{Document}{Placemark};
given ($city_name) {
    when ('north-vancouver') {
        $placemarks = $kml->{Document}{Folder}{Folder}{Placemark};
        $area_name = 'Vancouver';
    }
    when ('edmonton') {
        $placemarks = $kml->{Document}{Placemark};
    }
    when ('burnaby') {
        $area_name = 'Vancouver',
    }
}

for my $shape_name (keys %$placemarks) {
    my $place = $placemarks->{$shape_name};
    my $name = $shape_name;
    my $nice_name = $name;
    my $style;
    given ($city_name) {
        when ('victoria') {
            my $desc = $place->{description};
            if ($desc
                =~ m{www\.victoria\.ca/common/pdfs/garbage-(\w+)-schedule}) {
                $name      = "victoria-$1";
                $nice_name = "Victoria " . ucfirst($1);
            }
            else {
                warn "No idea about: $desc";
            }
        }
        when ([qw/esquimalt oakbay/]) {
            $name      =~ s/\d$//;
            $nice_name =~ s/-/ /;
            $nice_name = join ' ', map {ucfirst} split ' ', $nice_name;
            $area_name = 'Victoria';
        }
        when ('toronto') {
            $name =~ s/(-\d)-\d$/$1/;
            if ($name =~ m/^(toronto-d(\d)-(\w+)-([12]))/) {
                $name = $1;
                $nice_name = "Toronto District $2 " . ucfirst($3) . " $4";
                $style = "$3$4";
            }

        }
        when ('north-vancouver') {
            my $nv_id
                = $place->{ExtendedData}{SchemaData}{SimpleData}{Garbage_Zone}
                {content};
            my %nv_colours = (
                1 => 'yellow',
                2 => 'green',
                3 => 'blue',
                4 => 'pink',
                5 => 'purple',
            );
            $style = $nv_colours{$nv_id};
            $name  = "north-vancouver-$nv_id-$style";
            $nice_name
                = "North Vancouver " . ucfirst($style) . " (Zone $nv_id)";
        }
        when ('edmonton') {
            my $simpledata = $place->{ExtendedData}{SchemaData}{SimpleData};
            my $zone = $simpledata->{ZONE}{content};
            my $day = $simpledata->{DAY}{content};
            $zone =~ s/^Zone //;
            $day  =~ s/^Day //;
            $name = "edmonton-$zone$day";
            $nice_name = "Edmonton Zone $zone$day";
            $style = 'blue';
        }
        when ('burnaby') {
            ($nice_name = $name) =~ s/^burnaby-(\w)/"Burnaby Zone " . uc($1)/e;
            $style = $name;
        }
    }

    my $coords = $place->{Polygon}{outerBoundaryIs}{LinearRing}{coordinates};
    unless (defined $coords) {
        warn "Placemark $name has no polygon - skipping\n";
        next;
    }
    my @points;
    if ($coords =~ m/\n/s) {
        for my $line (split "\n", $coords) {
            next unless $line =~ m/\S/;
            next unless $line =~ m/^\s*([^,]+),([\d.-]+)/;
            push @points, "$2 $1";
        }
    }
    else {
        for my $line (split " ", $coords) {
            next unless $line =~ m/^\s*([^,]+),([\d.-]+)/;
            push @points, "$2 $1";
        }
    }
    die "Could not find points for $name!" unless @points;
    my $polygon = '((' . join(',', @points) . '))';
    unless ($style) {
        ($style = $place->{styleUrl}) =~ s/^#//;
    }
    #warn "Found zone $name, $nice_name - $style - $polygon\n";
    warn "Found zone $name, $nice_name - $style\n";

    push @{ $zones{$name} }, {
        polygon => $polygon,
        style => $style,
        nice_name => $nice_name,
    };
#    warn "$name has " . scalar(@{ $zones{$name} });
}

my %style;
if ($area_name eq 'Victoria') {
    my $styles = $kml->{Document}{Style};
    for my $name (keys %$styles) {
        my $s = $styles->{$name};
        $style{$name} = {
            linecolour => $s->{LineStyle}{color},
            polycolour => $s->{PolyStyle}{color},
        };
    }
}
elsif ($city_name eq 'north-vancouver') {
    %style = (
        yellow => { linecolour => 'FF34FFFF', polycolour => '9934FFFF' },
        green =>  { linecolour => 'FFA3D9A4', polycolour => '99A3D9A4' },
        blue =>   { linecolour => 'FFDFCC96', polycolour => '99DFCC96' },
        pink =>   { linecolour => 'FFBD9BFA', polycolour => '99BD9BFA' },
        purple => { linecolour => 'FFC088AD', polycolour => '99C088AD' },
    );
}
elsif ($city_name eq 'toronto') {
    %style = (
        tuesday1   => { linecolour => 'FF849A13', polycolour => '99849A13' },
        tuesday2   => { linecolour => 'FF72CD70', polycolour => '9972CD70' },
        wednesday1 => { linecolour => 'FF56ACFC', polycolour => '9956ACFC' },
        wednesday2 => { linecolour => 'FF14D1FC', polycolour => '9914D1FC' },
        thursday1  => { linecolour => 'FFA73800', polycolour => '99A73800' },
        thursday2  => { linecolour => 'FFDAAA74', polycolour => '99DAAA74' },
        friday1    => { linecolour => 'FFBB5172', polycolour => '99BB5172' },
        friday2    => { linecolour => 'FFD191B4', polycolour => '99D191B4' },
    );
}
elsif ($city_name eq 'burnaby') {
    %style = (
        'burnaby-a' => { linecolour => 'FFC7C9EE', polycolour => '99C7C9EE' },
        'burnaby-b' => { linecolour => 'FF70AA8B', polycolour => '9970AA8B' },
        'burnaby-c' => { linecolour => 'FFCCAE7E', polycolour => '99CCAE7E' },
        'burnaby-d' => { linecolour => 'FF31CCFF', polycolour => '9931CCFF' },
        'burnaby-e' => { linecolour => 'FFB4769E', polycolour => '99B4769E' },
    );
}
else {
    # Just define blue by default
    %style = (
        blue => {
            linecolour => 'FFDFCC96',
            polycolour => '99DFCC96',
        },
    );
}

my $sql_file = "etc/sql/$city_name.sql";
open my $fh, ">$sql_file" or die "Can't write to $sql_file";
print $fh <<EOT;
BEGIN;
-- This script ASSUMES that the area already exists

-- Delete any existing data for this area
DELETE FROM pickups
    WHERE zone_id IN (
        SELECT id FROM zones WHERE name like '$city_name-%'
    );
DELETE FROM zones WHERE name like '$city_name-%';

-- Insert the zones
EOT

for my $zone_name (keys %zones) {
    my $details = $zones{$zone_name}[0];
    my $style = $style{ $details->{style} }
        or die "Couldn't find style $details->{style} for $zone_name";
    $style->{name} = $details->{style};
    my $multipolygon
        = 'MULTIPOLYGON('
        . join(',', map { $_->{polygon} } @{ $zones{$zone_name} })
        . ")";
    print $fh <<EOT;
INSERT INTO zones (id, area_id, name, title, colour_name, line_colour, poly_colour)
    VALUES (nextval('zone_seq'),(SELECT id FROM areas WHERE name = '$area_name'),'$zone_name','$details->{nice_name}','$style->{name}','$style->{linecolour}','$style->{polycolour}');
UPDATE zones SET geom = ST_GeomFromText('$multipolygon') WHERE name = '$zone_name';

EOT
}

print $fh <<EOT;


-- Now the schedule data
EOT

my $csv_file = "data/$city_name.csv";
my $csv = Text::CSV_XS->new or die "Cannot use CSV: ".Text::CSV->error_diag;
open my $csv_fh, "<:encoding(utf8)", $csv_file or die "Can't open $csv_file: $!";
while (my $row = $csv->getline ($csv_fh)) {
    next if $row->[0] =~ m/^#/;
    next if $row->[0] =~ m/^\w+$/;
    my ($zone_name, $day, $flags, $utc) = @$row;
    $utc ||= $UTC;
    $flags = uc $flags;
    die "Couldn't find zone $zone_name in the CSV (@$row)" unless $zones{$zone_name};

    print $fh <<EOT;
INSERT INTO pickups (id, zone_id, day, flags)
    VALUES (nextval('pickup_seq'),
           (SELECT id FROM zones WHERE name = '$zone_name'),
           '$day 07:00:00$utc', '$flags');
EOT
}
close $csv_fh;

print $fh <<EOT;

COMMIT;
EOT

close $fh or die "Can't write to $sql_file: $!";
print "Wrote $sql_file\n";
exit;


__DATA__


Edmonton has DST from March 13 to November 6
* Summer is UTC -06, Winter is UTC -07
