#!/usr/bin/perl
use strict;
use warnings;
use FindBin;
use lib "$FindBin::Bin/../lib";
use Recollect::Reminder;
use Recollect::Zone;
use Recollect::Notifier;
use DateTime;
use Getopt::Long;

$ENV{RECOLLECT_TEST_CONFIG_FILE} = "$FindBin::Bin/../etc/recollect.yaml";
$ENV{RECOLLECT_BASE_PATH} = "$FindBin::Bin/..";
$Recollect::Roles::Config::CONFIG = undef;

my %args;
GetOptions( \%args,
    'zone=s',
    'asof=s',
);

my $zone;
if ($args{zone}) {
    $zone = Recollect::Zone->By_name($args{zone});
}
else {
    $zone = Recollect::Zone->All->[0];
}

my %notify_opts;
if (my $asof = $args{asof}) {
    $asof =~ m/^(\d{4})-(\d{2})-(\d{2})$/
        or die "Could not parse $asof as a date\n";
    my ($y, $m, $d) = ($1, $2, $3);
    $notify_opts{now} = DateTime->new(year => $y, month => $m, day => $d);
    $ENV{RECOLLECT_NOW} = $notify_opts{now};
}

my $target = shift or usage();
Recollect::Notifier->new->send_notification(
    zone => $zone,
    target => $target,
    %notify_opts,
);
exit;


sub usage {
    die <<EOT;
USAGE: $0 <target> [--zone]
EOT
}
